buildscript {
	repositories {
		jcenter()
		maven {url "http://files.minecraftforge.net/maven"}
		maven {url "https://plugins.gradle.org/m2/"}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7"
	}
}


apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

sourceCompatibility = targetCompatibility = 1.8

idea { module { inheritOutputDirs = true } }

String[] versionsMC = ["1.12"]
String versionForge = "14.21.1.2415"
String FLVersion = "2.2.1"
String MFReq = "MF_DEPS = \"required-after:fl@[${FLVersion},);after:jei@[4.7.0,)\""

task deobfJar(type: Jar, dependsOn: "reobfJar") {
	from sourceSets.main.output
	from sourceSets.api.output
	classifier = "deobf"
}
task sourcesJar(type: Jar, dependsOn: "sourceJar") {
	from (sourceSets.main.allJava) {
		include "szewek/mcflux/R.java"
		expand 'version': project.version
	}
	from (sourceSets.main.allJava) {
		exclude "szewek/mcflux/R.java"
	}
	from sourceSets.api.allJava
	from sourceSets.main.resources
	classifier = "sources"
}
String ttag = System.getenv("TRAVIS_TAG")
version = ttag ? ttag : "0.0.0-x"
group = "szewek.mcflux"
archivesBaseName = "mcflux"

minecraft {
	version = versionsMC[0] + "-" + versionForge
	runDir = "run"
	mappings = "snapshot_20170710"
	replace '\${version}', project.version
	replace "MF_DEPS = \"\"", MFReq
	replaceIn "R.java"
}

publishing {
	publications {
		MCFluxPublish(MavenPublication) {
			from components.java
			artifact deobfJar
			artifact sourcesJar
			groupId project.group
			artifactId project.archivesBaseName
			version project.version
		}
	}
}

repositories {
	maven {url "http://minecraft.curseforge.com/api/maven"}
	maven {url "http://maven.covers1624.net"}
	maven {url "http://dvs1.progwml6.com/files/maven"}
	//maven {url "http://mobiusstrip.eu/maven"}
	maven {url "http://maven.ic2.player.to"}
	maven {url "http://maven.epoxide.org"}
	maven {url "http://maven.tterrag.com"}
}

dependencies {
	deobfCompile "fl:fl:" + FLVersion
	deobfCompile "cofh:RedstoneFlux:1.12-2.0.1.2:universal"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.15-8:api"
	deobfCompile "mezz.jei:jei_1.12:4.7.2.77"
	//deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
	deobfCompile "net.industrial-craft:industrialcraft-2:2.6.54-ex110:api"
	deobfCompile "net.darkhax.tesla:Tesla-1.12:1.0.60"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand "version":project.version, "mcversion":project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
curseforge {
	String cfkey = System.getenv("CURSEFORGE_KEY")
	apiKey = cfkey ? cfkey : ""
	project {
		id = "248942"
		releaseType = "release"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")
		versionsMC.each {
			addGameVersion it
		}
		relations {
			requiredLibrary "fl"
			optionalLibrary "redstone-flux"
		}
		addArtifact sourcesJar
		addArtifact deobfJar
	}
}
artifacts {
	archives deobfJar
	archives sourcesJar
}