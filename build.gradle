buildscript {
	repositories {
		jcenter()
		maven {url "http://files.minecraftforge.net/maven"}
		maven {url "https://plugins.gradle.org/m2/"}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
		classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.9"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.30"
	}
}

apply plugin: "kotlin"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = "1.8"
	}
}

idea { module { inheritOutputDirs = true } }

String[] versionsMC = ["1.12.2"]
String versionForge = "14.23.1.2562"
String FLVersion = "2.20.6"
String MFReq = "MF_DEPS = \"required-after:fl@[${FLVersion},);after:jei@[4.7.0,)\""

task deobfJar(type: Jar, dependsOn: "reobfJar") {
	from sourceSets.main.output
	from sourceSets.api.output
	classifier = "deobf"
}
task sourcesJar(type: Jar, dependsOn: "sourceJar") {
	from (sourceSets.main.allSource) {
		include "szewek/mcflux/R.java"
		expand 'version': project.version
	}
	from (sourceSets.main.allSource) {
		exclude "szewek/mcflux/R.java"
	}
	from sourceSets.api.allSource
	from sourceSets.main.allSource
	classifier = "sources"
}
String ttag = System.getenv("TRAVIS_TAG")
version = ttag ? ttag : "0.0.0-x"
group = "szewek.mcflux"
archivesBaseName = "mcflux"

minecraft {
	version = versionsMC[0] + "-" + versionForge
	runDir = "run"
	mappings = "snapshot_20180301"
	replace '\${version}', project.version
	replace "MF_DEPS = \"required-after:fl\"", MFReq
	replaceIn "R.java"
}

repositories {
	jcenter()
	maven {url "http://maven.shadowfacts.net/"}
	maven {url "http://minecraft.curseforge.com/api/maven"}
	maven {url "http://maven.covers1624.net"}
	maven {url "http://dvs1.progwml6.com/files/maven"}
	//maven {url "http://mobiusstrip.eu/maven"}
	maven {url "http://maven.ic2.player.to"}
	maven {url "http://maven.tterrag.com"}
}

dependencies {
	compile group: "net.shadowfacts", name: "Forgelin", version: "1.6.0"
	deobfCompile "fl:fl:" + FLVersion
	deobfCompile "cofh:RedstoneFlux:1.12-2.0.1.2:universal"
	deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.15-8:api"
	deobfCompile "mezz.jei:jei_1.12:4.7.2.77"
	//deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
	deobfCompile "net.industrial-craft:industrialcraft-2:2.8.27-ex112:api"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand "version":project.version, "mcversion":project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}
curseforge {
	String cfkey = System.getenv("CURSEFORGE_KEY")
	apiKey = cfkey ? cfkey : ""
	project {
		id = "248942"
		releaseType = "beta"
		changelogType = "markdown"
		changelog = file("CHANGELOG.md")
		versionsMC.each {
			addGameVersion it
		}
		relations {
			requiredLibrary "shadowfacts-forgelin"
			requiredLibrary "fl"
			optionalLibrary "redstone-flux"
		}
		addArtifact sourcesJar
		addArtifact deobfJar
	}
}
artifacts {
	archives deobfJar
	archives sourcesJar
}